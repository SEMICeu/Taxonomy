PREFIX apf: <http://jena.apache.org/ARQ/property#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skos-xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX gleif-base: <https://www.gleif.org/ontology/Base/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?ELF ?OP ?MinDistance ?country
  WHERE {
{
SELECT ?ELF (MIN(?Distance) AS ?JMin)
WHERE
{
    SERVICE <x-sparql-anything:2023-09-28-elf-code-list-v1.5.csv,csv.headers=true> {
    
      # extract values from the respective columns of the CSV file
      ?elf xyz:Country%20of%20formation ?ELF .
      FILTER(?ELF !="") .
    
    }
    SERVICE <x-sparql-anything:countries-skos-en.ttl> {
    
      # extract the URI of the countries with their English label 
      ?country rdf:type skos:Concept .
      ?country skos:prefLabel ?label .      
       BIND (STR(?label)  AS ?eng_label) .
      OPTIONAL {
           ?country skos:altLabel ?altLabel .      
       BIND (STR(?altLabel)  AS ?alt_label) .
        }

    }
  BIND ( fx:CosineDistance(?ELF, ?eng_label) AS ?Distance1) .
  BIND(IF(BOUND(?alt_label), fx:CosineDistance(?ELF, ?alt_label), ?Distance1 ) AS ?Distance2) .
      BIND(IF(?Distance1 <= ?Distance2, ?Distance1, ?Distance2) AS ?Distance) .
}
GROUP BY ?ELF 
ORDER BY ASC(?JMin)
  }
  
  
  SERVICE <x-sparql-anything:countries-skos-en.ttl> {
    
      # extract the URI of the countries with their English label 
      ?country rdf:type skos:Concept .
      ?country skos:prefLabel ?label .      
       BIND (STR(?label)  AS ?OP) .
      OPTIONAL {
           ?country skos:altLabel ?altLabel2 .      
       BIND (STR(?altLabel2)  AS ?alt_label2) .
        }

    }
  
  BIND ( fx:CosineDistance(?ELF, ?OP) AS ?MinDistance1)
 
  BIND(IF(BOUND(?alt_label2), fx:CosineDistance(?ELF, ?alt_label2), ?MinDistance1 ) AS ?MinDistance2) .
      BIND(IF(?MinDistance1 <= ?MinDistance2, ?MinDistance1, ?MinDistance2) AS ?MinDistance) .
  FILTER (?MinDistance = ?JMin)
}