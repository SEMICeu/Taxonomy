PREFIX apf: <http://jena.apache.org/ARQ/property#>
PREFIX at: <http://publications.europa.eu/ontology/authority/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skos-xl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX gleif-base: <https://www.gleif.org/ontology/Base/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?countryFormation ?eng_label2 ?MinDistance
  WHERE {
{
SELECT ?countryFormation (MIN(?JaroD) AS ?JMin)
WHERE
{
    SERVICE <x-sparql-anything:2023-09-28-elf-code-list-v1.5.csv,csv.headers=true> {
    
      # extract values from the respective columns of the CSV file
      ?elf xyz:Country%20of%20formation ?countryFormation .
      FILTER(?countryFormation !="") .
    
    }
    SERVICE <x-sparql-anything:countries-skos.rdf> {
    
      # extract the URI of the countries with their English label 
      ?country rdf:type skos:Concept .
      ?country skos:prefLabel ?label .      
       BIND (STR(?label)  AS ?eng_label) .
      FILTER (lang(?label) = 'en') .
      
      # filter out those countries that are active, otherwise you can get multiple URI
      FILTER NOT EXISTS {?country at:end.use ?endUse} .

    }
  BIND ( fx:JaroWinklerDistance(?countryFormation, ?eng_label) AS ?JaroD)
  
}
GROUP BY ?countryFormation 
ORDER BY ASC(?JMin)
  }
  
  
  SERVICE <x-sparql-anything:countries-skos.rdf> {
    
      # extract the URI of the countries with their English label 
      ?country rdf:type skos:Concept .
      ?country skos:prefLabel ?label .      
       BIND (STR(?label)  AS ?eng_label2) .
      FILTER (lang(?label) = 'en') .
      
      # filter out those countries that are active, otherwise you can get multiple URI
      FILTER NOT EXISTS {?country at:end.use ?endUse} .

    }
  
  BIND ( fx:JaroWinklerDistance(?countryFormation, ?eng_label2) AS ?MinDistance)
  FILTER (?MinDistance = ?JMin)
}